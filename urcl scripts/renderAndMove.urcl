bits == 8
minreg 7
minheap 4

@define x m0 //replace with 0x00 if m0 not supported in translator
@define y m1
@define z m2
@define rot m3

str x 0x18
str y 0x38
str z 0x18
str rot 0x00

//TODO: update this script to use the new %playerinput port

out %playerinput r0

.loop

    in r0 %amogus_clearbuffer

    //handle inputs
        in r0 %playerinput //get input
        in r0 %playerinput //inventory movement
        in r0 %playerinput //is break pressed?
        in r0 %playerinput //is place pressed?

        in r1 %playerinput //is crouch pressed?
        bev ~+4 r1
            lod r1 y
            sub r1 r1 8
            str y r1

        in r1 %playerinput //rotation

        lod r2 rot
        addv r2 r2 r1
        str rot r2

        in r1 %playerinput //forward movement
        mov r2 r1 //r2 = forward movement
        in r3 %amogus_sinyaw //r3 = player direction x component
        neg r3 r3
        cal .moveHelperFunc //r4 = player forward movement x component
        //r2, r3, r5, r6 = junk
        lod r5 x
        add r4 r5 r4
        str x r4

        mov r2 r1 //r2 = forward movement
        in r3 %amogus_cosyaw //r3 = player direction z component
        cal .moveHelperFunc //r4 = player forward movement z component
        //r2, r3, r5, r6 = junk
        lod r5 z
        add r4 r5 r4
        str z r4

        in r1 %playerinput //strafe movement

        mov r2 r1 //r2 = strafe movement
        in r3 %amogus_sinyaw //r3 = player direction x component
        cal .moveHelperFunc //r4 = player strafe movement x component
        //r2, r3, r5, r6 = junk
        lod r5 z
        add r4 r5 r4
        str z r4

        mov r2 r1 //r2 = strafe movement
        in r3 %amogus_cosyaw //r3 = player direction z component
        cal .moveHelperFunc //r4 = player strafe movement z component
        //r2, r3, r5, r6 = junk
        lod r5 x
        add r4 r5 r4
        str x r4

        in r1 %playerinput
        bev ~+4 r1
            lod r1 y
            add r1 r1 8
            str y r1

    lod r1 x
    out %amogus_camx r1
    lod r1 y
    out %amogus_camy r1
    lod r1 z
    out %amogus_camz r1
    lod r1 rot
    out %amogus_camrot r1

    in r0 %meshgen_renderscene

    in r0 %amogus_drawtoscreen
    in r0 %screen_buffer

    out %wait 125
    in r0 %wait
jmp .loop

//moveHelperFunc
    //inputs:
        //r2: local movement vector component (not preserved)
        //r3: direction of component in global axis (not preserved)
    //outputs:
        //r4: component of movement in world axis
    //also uses:
        //r5 (not preserved)
        //r6 (not preserved)
.moveHelperFunc
    xor r6 r2 r3
    brp ~+2 r2
        neg r2 r2
    brp ~+2 r3
        neg r3 r3
    mlt r4 r2 r3
    umlt r5 r2 r3
    bsr r4 r4 4
    bsl r5 r5 4
    or r4 r4 r5
    brp ~+2 r6
        neg r4 r4
ret